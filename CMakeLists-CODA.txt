include(ExternalProject)

set(CODA_DIR ${CMAKE_CURRENT_SOURCE_DIR}/externals/coda-oss)
set(CODA_INSTALL_DIR ${CMAKE_INSTALL_PREFIX})
file(MAKE_DIRECTORY ${CMAKE_INSTALL_PREFIX}/include)

ExternalProject_Add(coda-oss
                    SOURCE_DIR ${CODA_DIR}
                    CONFIGURE_COMMAND ""
                    BUILD_COMMAND ""
                    INSTALL_COMMAND "")

if(CMAKE_BUILD_TYPE MATCHES Debug)
    set(configure_flags "--enable-swig\;--enable-debugging")
elseif(CMAKE_BUILD_TYPE MATCHES RelWithDebInfo)
    set(configure_flags "--enable-swig\;--enable-debugging")
else()
    set(configure_flags "--enable-swig")
endif()

ExternalProject_Add_Step(coda-oss
        waf-configure
        COMMAND ${Python_EXECUTABLE} waf configure --prefix=${CODA_INSTALL_DIR} "${configure_flags}"
        DEPENDERS build
        ALWAYS 0
        WORKING_DIRECTORY ${CODA_DIR})

ExternalProject_Add_Step(coda-oss
        waf-build
        COMMAND ${Python_EXECUTABLE} waf install
        --target=mt-c++,math-c++,io-c++,mem-c++,sys-c++,str-c++,except-c++,j2k_config,J2K,JPEG
        DEPENDEES waf-configure
        DEPENDERS build
        ALWAYS 0
        WORKING_DIRECTORY ${CODA_DIR})

if(Python_NumPy_FOUND)
    ExternalProject_Add_Step(coda-oss
            waf-build-numpyutils
            COMMAND ${Python_EXECUTABLE} waf install --target=numpyutils-c++
            DEPENDEES waf-configure
            DEPENDERS build
            ALWAYS 0
            WORKING_DIRECTORY ${CODA_DIR})

endif()
